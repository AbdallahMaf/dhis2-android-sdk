apply plugin: "findbugs"

findbugs {
    toolVersion = "3.0.1"
}

afterEvaluate {
    android.libraryVariants.each { variant ->

        def compileTask

        if (variant.hasProperty('javaCompileProvider')) {
            // Android 3.3.0+
            compileTask = variant.javaCompileProvider.get()
        } else {
            compileTask = variant.javaCompile
        }

        def findBugsTask = tasks.register("findBugs${variant.name.capitalize()}", FindBugs) {

            group = "verification"
            description = "Run FindBugs for the ${variant.name}"

            ignoreFailures = false
            effort = "max"
            reportLevel = "low"

            reports {
                html.enabled = true
                xml.enabled = false
            }

            excludeFilter = project.file(
                    "plugins/findbugs-filter.xml"
            )

            source = compileTask.source
            classes = fileTree(compileTask.destinationDir)
            classpath = compileTask.classpath.plus(project.files(android.bootClasspath))
        }

        findBugsTask.get().dependsOn(compileTask)

        if (tasks.named("check")) {
            check.dependsOn(findBugsTask)
        }
    }
}